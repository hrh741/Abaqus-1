      SUBROUTINE UMAT(STRESS, STATEV, DDSDDE, SSE, SPD, SCD, RPL, 
      1 DDSDDT, DRPLDE, DRPLDT, STRAN, DSTRAN, TIME, DTIME, TEMP, DTEMP, 
      2 PREDEF, DPRED, CMNAME, NDI, NSHR, NTENS, NSTATV, PROPS, NPROPS, 
      3 COORDS, DROT, PNEWDT, CELENT, DFGRD0, DFGRD1, NOEL, NPT, LAYER, 
      4 KSPT, KSTEP, KINC) 
C 
      INCLUDE �ABA_PARAM.INC� 
C 
      CHARACTER*8 CMNAME 
C 
      DIMENSION STRESS(NTENS), STATEV(NSTATV), DDSDDE(NTENS, NTENS), 
      1 DDSDDT(NTENS), DRPLDE(NTENS), STRAN(NTENS), DSTRAN(NTENS), 
      2 PREDEF(1), DPRED(1), PROPS(NPROPS), COORDS(3), DROT(3, 3), 
      3 DFGRD0(3, 3), DFGRD1(3, 3) 
C LOCAL ARRAYS 
C ---------------------------------------------------------------- 
C EELAS - ELASTIC STRAINS 
C ETHERM - THERMAL STRAINS 
C DTHERM - INCREMENTAL THERMAL STRAINS 
C DELDSE - CHANGE IN STIFFNESS DUE TO TEMPERATURE CHANGE 
C ---------------------------------------------------------------- 
      DIMENSION EELAS(6), ETHERM(6), DTHERM(6), DELDSE(6,6) 
C 
      PARAMETER(ZERO=0.D0, ONE=1.D0, TWO=2.D0, THREE=3.D0, SIX=6.D0) 
C ---------------------------------------------------------------- 
C UMAT FOR ISOTROPIC THERMO-ELASTICITY WITH LINEARLY VARYING 
C MODULI - CANNOT BE USED FOR PLANE STRESS 
C ---------------------------------------------------------------- 
C PROPS(1) - E(T0) 
C PROPS(2) - NU(T0) 
C PROPS(3) - T0 
C PROPS(4) - E(T1) 
C PROPS(5) - NU(T1) 
C PROPS(6) - T1 
C PROPS(7) - ALPHA 
C PROPS(8) - T_INITIAL 
C ELASTIC PROPERTIES AT START OF INCREMENT 
C 
      FAC1=(TEMP-PROPS(3))/(PROPS(6)-PROPS(3)) 
      IF (FAC1 .LT. ZERO) FAC1=ZERO 
      IF (FAC1 .GT. ONE) FAC1=ONE 
      FAC0=ONE-FAC1 
      EMOD=FAC0*PROPS(1)+FAC1*PROPS(4) 
      ENU=FAC0*PROPS(2)+FAC1*PROPS(5) 
      EBULK3=EMOD/(ONE-TWO*ENU) 
      EG20=EMOD/(ONE+ENU) 
      EG0=EG20/TWO 
      ELAM0=(EBULK3-EG20)/THREE 
C 
C ELASTIC PROPERTIES AT END OF INCREMENT 
C 
      FAC1=(TEMP+DTEMP-PROPS(3))/(PROPS(6)-PROPS(3)) 
      IF (FAC1 .LT. ZERO) FAC1=ZERO 
      IF (FAC1 .GT. ONE) FAC1=ONE 
      FAC0=ONE-FAC1 
      EMOD=FAC0*PROPS(1)+FAC1*PROPS(4) 
      ENU=FAC0*PROPS(2)+FAC1*PROPS(5) 
      EBULK3=EMOD/(ONE-TWO*ENU) 
      EG2=EMOD/(ONE+ENU) 
      EG=EG2/TWO 
      ELAM=(EBULK3-EG2)/THREE 
C 
C ELASTIC STIFFNESS AT END OF INCREMENT AND STIFFNESS CHANGE 
C 
      DO K1=1,NDI 
      DO K2=1,NDI 
      DDSDDE(K2,K1)=ELAM 
      DELDSE(K2,K1)=ELAM-ELAM0 
      END DO 
      DDSDDE(K1,K1)=EG2+ELAM 
      DELDSE(K1,K1)=EG2+ELAM-EG20-ELAM0 
      END DO 
      DO K1=NDI+1,NTENS 
      DDSDDE(K1,K1)=EG 
      DELDSE(K1,K1)=EG-EG0 
      END DO 
C 
C CALCULATE THERMAL EXPANSION 
C 
      DO K1=1,NDI 
      ETHERM(K1)=PROPS(7)*(TEMP-PROPS(8)) 
      DTHERM(K1)=PROPS(7)*DTEMP 
      END DO 
      DO K1=NDI+1,NTENS 
      ETHERM(K1)=ZERO 
      DTHERM(K1)=ZERO 
      END DO 
C 
C CALCULATE STRESS, ELASTIC STRAIN AND THERMAL STRAIN 
C 
      DO K1=1, NTENS 
      DO K2=1, NTENS 
      STRESS(K2)=STRESS(K2)+DDSDDE(K2,K1)*(DSTRAN(K1)-DTHERM(K1)) 
     1 +DELDSE(K2,K1)*( STRAN(K1)-ETHERM(K1)) 
      END DO 
      ETHERM(K1)=ETHERM(K1)+DTHERM(K1) 
      EELAS(K1)=STRAN(K1)+DSTRAN(K1)-ETHERM(K1) 
      END DO 
C 
C STORE ELASTIC AND THERMAL STRAINS IN STATE VARIABLE ARRAY 
C 
      DO K1=1, NTENS 
      STATEV(K1)=EELAS(K1) 
      STATEV(K1+NTENS)=ETHERM(K1) 
      END DO 
      RETURN 
      END
